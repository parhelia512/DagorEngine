from "%darg/ui_imports.nut" import *
from "%sqstd/frp.nut" import *
from "types" import Array

function Bar(has_scroll) {
  if (has_scroll) {
    return {
      rendObj = ROBJ_SOLID
      color = Color(40, 40, 40, 160)
      _width = sh(1)
      _height = sh(1)
    }
  }
  else return {
      _width = sh(1)
      _height = sh(1)
  }
}
let Knob = freeze({
  rendObj = ROBJ_SOLID
  colorCalc = @(sf) sf & S_ACTIVE ? Color(255,255,255)
    : sf & S_HOVER ? Color(110, 120, 140, 80)
    : Color(110, 120, 140, 160)
})

const ContentRoot = {
  size = flex()
}


function calcBarSize(bar_style, axis) {
  return axis == 0 ? [flex(), bar_style._height] : [bar_style._width, flex()]
}


function scrollbar(scroll_handler, options={}) {
  let stateFlags = Watched(0)

  let orientation = options?.orientation ?? O_VERTICAL
  let axis        = orientation == O_VERTICAL ? 1 : 0

  return function() {
    let elem = scroll_handler.elem

    if (!elem) {
      let cls = Bar(false)
      return cls.__merge({
        key = scroll_handler
        behavior = Behaviors.Slider
        watch = scroll_handler
        size = (options?.needReservePlace ?? true) ? calcBarSize(cls, axis) : null
      })
    }

    local contentSize, elemSize, scrollPos
    if (axis == 0) {
      contentSize = elem.getContentWidth()
      elemSize = elem.getWidth()
      scrollPos = elem.getScrollOffsX()
    } else {
      contentSize = elem.getContentHeight()
      elemSize = elem.getHeight()
      scrollPos = elem.getScrollOffsY()
    }

    if (contentSize <= elemSize) {
      let cls = Bar(false)
      return cls.__merge({
        key = scroll_handler
        behavior = Behaviors.Slider
        watch = scroll_handler
        size = (options?.needReservePlace ?? true) ? calcBarSize(cls, axis) : null
      })
    }


    const minV = 0
    let maxV = contentSize - elemSize
    let fValue = scrollPos

    let knob = Knob.__merge({
      size = [flex(elemSize), flex(elemSize)]
      color = Knob.colorCalc(stateFlags.get())
      key = "knob"
    })

    let cls = Bar(true)
    return cls.__merge({
      key = scroll_handler
      behavior = Behaviors.Slider

      watch = [scroll_handler, stateFlags]
      fValue = fValue

      knob
      min = minV //warning disable : -ident-hides-std-function
      max = maxV //warning disable : -ident-hides-std-function
      unit = 1

      flow = axis == 0 ? FLOW_HORIZONTAL : FLOW_VERTICAL
      halign = ALIGN_CENTER
      valign = ALIGN_CENTER

      pageScroll = (axis == 0 ? -1 : 1) * (maxV - minV) / 100.0 // TODO probably needed sync with container wheelStep option

      orientation = orientation
      size = calcBarSize(cls, axis)

      children = [
        {size=[flex(fValue), flex(fValue)]}
        knob
        {size=[flex(maxV-fValue), flex(maxV-fValue)]}
      ]

      onChange = @(val) axis == 0
        ? scroll_handler.scrollToX(val)
        : scroll_handler.scrollToY(val)

      onElemState = @(sf) stateFlags.set(sf)
    })
  }
}

let DEF_SIDE_SCROLL_OPTIONS = { //const
  rootBase = null
  scrollAlign = ALIGN_RIGHT
  orientation = O_VERTICAL
  size = flex()
  maxWidth = null
  maxHeight = null
  needReservePlace = true //need reserve place for scrollbar when it not visible
  clipChildren  = true
  joystickScroll = true
  // when virtualItems is set, the content root builds only the visible items
  // (see Behaviors.VirtualList) and 'content' is ignored. An observable here
  // makes the content root watch it. The rest left null keeps the behavior's
  // own default, so this does not name it twice
  virtualItems = null
  virtualItemHeight = null
  virtualItemHeights = null
  virtualOverscan = null
  virtualInitialCount = null
  virtualTail = null
}

function makeSideScroll(content, options = DEF_SIDE_SCROLL_OPTIONS) {
  options = DEF_SIDE_SCROLL_OPTIONS.__merge(options)

  let scrollHandler = options?.scrollHandler ?? ScrollHandler()
  let rootBase = options.rootBase ?? ContentRoot
  let scrollAlign = options.scrollAlign

  function contentRoot() {
    local bhv = rootBase?.behavior ?? []
    if (!(bhv instanceof Array))
      bhv = [bhv]
    else
      bhv = clone bhv
    bhv.append(Behaviors.WheelScroll, Behaviors.ScrollEvent)
    let vitems = options.virtualItems
    let virtual = vitems != null
    if (virtual)
      bhv.append(Behaviors.VirtualList)

    let rootDesc = rootBase.__merge({
      size = options.size
      behavior = bhv
      scrollHandler = scrollHandler
      wheelStep = 0.8
      orientation = options.orientation
      joystickScroll = options.joystickScroll
      maxHeight = options.maxHeight
      maxWidth = options.maxWidth
    })
    if (!virtual)
      return rootDesc.__merge({ children = content })

    // VirtualList takes the items itself; passing 'children' too would shift
    // the item indices it maps focus and windows onto
    let virtualDesc = rootDesc.__merge({
      // the flow axis has to be the scroll axis: VirtualList stacks the
      // items and the skipped-run spacers along it, and the default
      // ContentRoot declares no flow at all
      flow = options.orientation == O_VERTICAL ? FLOW_VERTICAL : FLOW_HORIZONTAL
      virtualItems = isObservable(vitems) ? vitems.get() : vitems
      virtualItemHeight = options.virtualItemHeight
      virtualItemHeights = options.virtualItemHeights
      virtualOverscan = options.virtualOverscan
      virtualInitialCount = options.virtualInitialCount
      virtualTail = options.virtualTail
    })
    if (isObservable(vitems)) {
      local watch = rootBase?.watch ?? []
      watch = watch instanceof Array ? clone watch : [watch]
      watch.append(vitems)
      virtualDesc.watch <- watch
    }
    return virtualDesc
  }

  let childrenContent = scrollAlign == ALIGN_LEFT || scrollAlign == ALIGN_TOP
    ? [scrollbar(scrollHandler, options), contentRoot]
    : [contentRoot, scrollbar(scrollHandler, options)]

  return {
    size = options.size
    maxHeight = options.maxHeight
    maxWidth = options.maxWidth
    flow = (options.orientation == O_VERTICAL) ? FLOW_HORIZONTAL : FLOW_VERTICAL
    clipChildren = options.clipChildren

    children = childrenContent
  }
}


function makeHVScrolls(content, options={}) {
  let scrollHandler = options?.scrollHandler ?? ScrollHandler()
  let rootBase = options?.rootBase ?? ContentRoot

  function contentRoot() {
    local bhv = rootBase?.behavior ?? []
    if (!(bhv instanceof Array))
      bhv = [bhv]
    else
      bhv = clone bhv
    bhv.append(Behaviors.WheelScroll, Behaviors.ScrollEvent)

    return rootBase.__merge({
      behavior = bhv
      scrollHandler = scrollHandler
      joystickScroll = true

      children = content
    })
  }

  return {
    size = flex()
    flow = FLOW_VERTICAL

    children = [
      {
        size = flex()
        flow = FLOW_HORIZONTAL
        clipChildren = true
        children = [
          contentRoot
          scrollbar(scrollHandler, options.__merge({orientation=O_VERTICAL}))
        ]
      }
      scrollbar(scrollHandler, options.__merge({orientation=O_HORIZONTAL}))
    ]
  }
}


function makeVertScroll(content, options={}) {
  let o = clone options
  o.orientation <- O_VERTICAL
  o.scrollAlign <- o?.scrollAlign ?? ALIGN_RIGHT
  return makeSideScroll(content, o)
}


function makeHorizScroll(content, options={}) {
  let o = clone options
  o.orientation <- O_HORIZONTAL
  o.scrollAlign <- o?.scrollAlign ?? ALIGN_BOTTOM
  return makeSideScroll(content, o)
}


return {
  scrollbar
  makeHorizScroll
  makeVertScroll
  makeHVScrolls
  makeSideScroll
}
